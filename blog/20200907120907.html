<!DOCTYPE html>
<html>
  <head>
    <!-- meta -->
<meta charset="UTF-8">
<meta name="viewport" content="width=device-width,initial-scale=1">
<link rel="icon" type="image/x-icon" href="/images/deeplearning-logo.png">
<link rel="stylesheet" type="text/css" href="/css/common.css">

<title>MNIST画像情報を読み込む - Perlで学ぶディープラーニング入門 - 基礎と実用が学べる国産AIプロダクト</title>
<meta name="description" content="MNIST画像情報をPerlで読み込むサンプルプログラムです。MNISTは、ディープラーニングで利用できる画像のサンプルです。">
  </head>
  <body>
    <div class="container">
      <div class="header">
        <h1>
  <a href="/">Perlで学ぶディープラーニング入門</a>
</h1>

      </div>
      <div class="main">
        <div class="content">
          <div class="entry">
  <div class="top">
    <!-- top -->

  </div>
  <div class="middle">
    <h2><a href="/blog/20200907120907.html">MNIST画像情報を読み込む</a></h2>
<p>
  MNIST画像情報をPerlで読み込むサンプルプログラムです。MNISTは、ディープラーニングで利用できる画像のサンプルです。
</p>
<p>
  <a href="http://yann.lecun.com/exdb/mnist/">THE MNIST DATABASE of handwritten digits</a>を読み込むサンプルです。
</p>
<pre>
use strict;
use warnings;
use FindBin;

# MNIST画像情報を読み込む
my $mnist_image_file = "$FindBin::Bin/data/train-images-idx3-ubyte";

open my $mnist_image_fh, '&lt;', $mnist_image_file
  or die "Can't open file $mnist_image_file: $!";

# マジックナンバー
my $image_buffer;
read($mnist_image_fh, $image_buffer, 4);
my $magic_number = unpack('N1', $image_buffer);
if ($magic_number != 0x00000803) {
  die "Invalid magic number expected " . 0x00000803 . "actual $magic_number";
}

# 画像数
read($mnist_image_fh, $image_buffer, 4);
my $items_count = unpack('N1', $image_buffer);

# 画像の行ピクセル数
read($mnist_image_fh, $image_buffer, 4);
my $rows_count = unpack('N1', $image_buffer);

# 画像の列ピクセル数
read($mnist_image_fh, $image_buffer, 4);
my $columns_count = unpack('N1', $image_buffer);

# 画像の読み込み
my $image_data;
my $all_images_length = $items_count * $rows_count * $columns_count;
my $read_length = read $mnist_image_fh, $image_data, $all_images_length;
unless ($read_length == $all_images_length) {
  die "Can't read all images";
}

# 画像情報
my $image_info = {};
$image_info-&gt;{items_count} = $items_count;
$image_info-&gt;{rows_count} = $rows_count;
$image_info-&gt;{columns_count} = $columns_count;
$image_info-&gt;{data} = $image_data;

</pre>
<h3>MNIST画像情報を読み込むプログラムの簡易解説</h3>
<p>
  「data」以下に「train-images-idx3-ubyte」を配置します。
</p>
<p>
  ファイルをオープンしてread関数で読み込んでいきます。
</p>
<p>
  マジックナンバーのチェックを行います。ビッグエンディアンの32bit整数なので、unpackに「N1」を指定します。
</p>
<p>
  画像数を取得します。ビッグエンディアンの32bit整数なので、unpackに「N1」を指定します。
</p>
<p>
  画像の行ピクセル数を取得します。ビッグエンディアンの32bit整数なので、unpackに「N1」を指定します。
</p>
<p>
  画像の列ピクセル数を取得します。ビッグエンディアンの32bit整数なので、unpackに「N1」を指定します。
</p>
<p>
  MNIST画像情報では、すべての画像データで、行ピクセル数と列ピクセル数は共通です。
</p>
<p>
  すべての画像を読み込みます。
</p>
<p>
  訓練用の画像情報である「train-images-idx3-ubyte」を読み込んでいますが、検証用の画像情報「t10k-images-idx3-ubyte」も同様の方法で読み込めます。
</p>
<h3>MNIST画像情報を出力する方法</h3>
<p>
  メモリ上にMNIST画像情報を読み込みましたが、これを確認する方法を解説します。ビットマップ画像として出力すれば、内容を確認できます。
</p>
<p>
  <a href="https://tutorial.perlzemi.com/blog/20110820131832.html">Imager</a>というモジュールを使います。CPANモジュールですので、cpanmあるいはcpanでインストールしてください。
</p>
<p>
  以下は、最初の5つの画像をビットマップ画像として出力するサンプルです。
</p>
<pre>
use strict;
use warnings;
use FindBin;
use Imager;

# MNIST画像情報を読み込む
my $mnist_image_file = "$FindBin::Bin/data/train-images-idx3-ubyte";

open my $mnist_image_fh, '&lt;', $mnist_image_file
  or die "Can't open file $mnist_image_file: $!";

# マジックナンバー
my $image_buffer;
read($mnist_image_fh, $image_buffer, 4);
my $magic_number = unpack('N1', $image_buffer);
if ($magic_number != 0x00000803) {
  die "Invalid magic number expected " . 0x00000803 . "actual $magic_number";
}

# 画像数
read($mnist_image_fh, $image_buffer, 4);
my $items_count = unpack('N1', $image_buffer);

# 画像の行ピクセル数
read($mnist_image_fh, $image_buffer, 4);
my $rows_count = unpack('N1', $image_buffer);

# 画像の列ピクセル数
read($mnist_image_fh, $image_buffer, 4);
my $columns_count = unpack('N1', $image_buffer);

# 画像の読み込み
my $image_data;
my $all_images_length = $items_count * $rows_count * $columns_count;
my $read_length = read $mnist_image_fh, $image_data, $all_images_length;
unless ($read_length == $all_images_length) {
  die "Can't read all images";
}

# 画像情報
my $image_info = {};
$image_info-&gt;{items_count} = $items_count;
$image_info-&gt;{rows_count} = $rows_count;
$image_info-&gt;{columns_count} = $columns_count;
$image_info-&gt;{data} = $image_data;

# 画像情報の出力
for (my $i = 0; $i &lt; 5; $i++) {

  # 画像オフセット
  my $offset = $i * $rows_count * $columns_count;

  # キャンバス(モノクロ)
  my $img = Imager-&gt;new(xsize =&gt; $rows_count, ysize =&gt; $columns_count, channels =&gt; 1);
  
  # 画像情報を順番に出力
  for (my $row = 0; $row &lt; $rows_count; $row++) {
    for (my $column = 0; $column &lt; $columns_count; $column++) {
      
      # 色(白黒がRGBと逆なので反転)
      my $pos = $offset + ($column * $rows_count) + $row;
      my $color_bin = substr($image_data, $pos, 1);
      my $color_value = unpack('C1', $color_bin);
      my $color_value_neg = $color_value ^ 0xFF;
      my $color = Imager::Color-&gt;new($color_value_neg, $color_value_neg, $color_value_neg);
      
      # ピクセル描画
      $img-&gt;setpixel(x =&gt; $row, y =&gt; $column, color =&gt; $color);
    }
  }
  # Web表示できるようにPNGとして保存
  my $bitmap_file = "$FindBin::Bin/tmp_images/number$i.png";
  $img-&gt;write(file =&gt; $bitmap_file);
}

</pre>
<p>
  出力した画像です。
</p>
<p>
  <img src="/images/mnist/handwritten_digits/number0.png"> <img src="/images/mnist/handwritten_digits/number1.png"> <img src="/images/mnist/handwritten_digits/number2.png"> <img src="/images/mnist/handwritten_digits/number3.png"> <img src="/images/mnist/handwritten_digits/number4.png">
</p>
<h3>MNIST画像情報を出力するプログラムの簡易解説</h3>
<p>
  Imagerオブジェクトを幅と高さとチャンネル数を指定して作成します。モノクロなので1を指定しています。
</p>
<p>
  画像情報を読み込んでいきます。行列の順で色の濃さが並んでいるので、取得します。データは、符号なし8bit整数で格納されているのでunpackに「C1」を指定しています。
</p>
<p>
  MNISTの色の濃さはRGBと逆なので、反転させています。
</p>
<p>
  Imager::Colorオブジェクトを作成しています。
</p>
<p>
  ピクセルに描画します。
</p>
<p>
  最後に、Webで表示できるようにpngで保存しています。拡張子から自動判定されます。
</p>
<p>
  C言語のlibpngライブラリがOSのインストールされていることと、Imager::File::PNGが必要かもしれません。
</p>

  </div>
  <div class="bottom">
    <!-- bottom -->

<iframe width="560" height="315" src="https://www.youtube.com/embed/dTKY0kor50A" frameborder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>

  </div>
</div>

        </div>
        <div class="side">
          <!-- side -->
<div class="side-list">
  <div class="side-list-title">
    Side Bar
  </div>
  <ul>
    <li><a href="/list.html">Entries</a></li>
    <li>Bar</li>
    <li>Baz</li>
  </ul>
</div>

        </div>
      </div>
      <div class="footer">
        <div class="kimoto_system_link">
  <a href="https://kimoto-system.co.jp/message">
    <img src="https://kimoto-system.co.jp/images/kimoto-system-logo.png" alt="Perl Webシステム開発">
    Perl Webシステム開発の木本システム
  </a>
</div>

<div class="copyright">
  Copyright © Yuki Kimoto
</div>

      </div>
    </div>
  </body>
</html>
